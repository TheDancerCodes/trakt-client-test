apply plugin: 'com.android.application'

def versions = [

    //  SDK and Tools
    compileSdk       : 23,
    buildTools       : '23.0.2',

    minSdk           : 16,
    targetSdk        : 23,

    code             : 1,
    name             : '1.0.0',

    //  Libraries
    supportLib       : '23.3.0',

    okHttp           : '3.2.0',
    retrofit         : '2.0.1',
    okHttpInterceptor: '3.0.1',
    gson             : '2.5',

    picasso          : '2.5.2',
    butterKnife      : '7.0.1',
    materialProgress : '1.1.5',

    jUnit            : '4.12',
    mockito          : '2.0.2-beta',

    supportTestRunner: '0.4.1',
    espresso         : '2.2.1',
]

repositories {
    mavenCentral()
    maven {
        url 'http://dl.bintray.com/amulyakhare/maven'
    }
}

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "me.walmyrcarvalho.recrutamentoandroid"

        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        versionCode versions.code
        versionName versions.name

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        buildConfigField "String", "API_KEY", "\"" + getAPIKey() + "\""
    }

    buildTypes {
        debug {
            debuggable true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Support Libraries
    compile "com.android.support:support-annotations:$versions.supportLib"
    compile "com.android.support:appcompat-v7:$versions.supportLib"
    compile "com.android.support:design:$versions.supportLib"
    compile "com.android.support:recyclerview-v7:$versions.supportLib"

    // Networking
    compile "com.squareup.okhttp3:okhttp:$versions.okHttp"
    compile "com.squareup.okhttp3:logging-interceptor:$versions.okHttpInterceptor"
    compile "com.squareup.retrofit2:retrofit:$versions.retrofit"
    compile "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    compile "com.google.code.gson:gson:$versions.gson"

    // Misc
    compile "com.squareup.picasso:picasso:$versions.picasso"
    compile "com.jakewharton:butterknife:$versions.butterKnife"
    compile "me.zhanghai.android.materialprogressbar:library:$versions.materialProgress"

    // Unit Testing
    testCompile "junit:junit:$versions.jUnit"
    testCompile "org.mockito:mockito-all:$versions.mockito"

    // UI Testing
    androidTestCompile "com.android.support:support-annotations:$versions.supportLib"

    androidTestCompile "com.android.support.test:runner:$versions.supportTestRunner"
    androidTestCompile "com.android.support.test:rules:$versions.supportTestRunner"

    androidTestCompile "com.android.support.test.espresso:espresso-core:$versions.espresso"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$versions.espresso") {
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
    }
}
